PSP_TOP			EQU 0x20005000 ;Вершина пользовательского стека
ICSR_ADDR       EQU 0xE000ED04 ;Регистр контроля состояния прерываний
PENDSVSET		EQU	0x10000000 ;pendSV бит в регистре контроля состояния прерываний
	
 AREA    |.text|, CODE, READONLY

PendSV_Handler	PROC
				EXPORT PendSV_Handler	
				TST	LR,	#0x4		;Проверяем бит 2 значения EXC_RETURN в LR 
;				ITE EQ				;Если ноль ("равно"), то был использован 
;				MRSEQ	R0,	MSP 	;основной стек, помещаем MSP в R0 
;				MRSNE	R0,	PSP 	;Иначе был использован стек процесса(Шла отрисовка), 
									;помещаем PSP в R0 
				BNE from_psp		;Убиваем отрисовку возвращаясь в MSP
from_msp
				TST	LR,	#0x8		;Проверяем бит 3 значения EXC_RETURN в LR 
;				ITE EQ				;Если ноль ("равно"), то пришли из обработчика 
				BEQ from_int		;Убиваем отрисовку возвращаясь в MSP
				
				LDR R0, =PSP_TOP	;Загрузили адрес пользовательского стека
				
				SUB R0, R0, #0x20	;Резервируем место в стеке под возврат из прерывания(8 двойных слов)
				MSR PSP, R0			;Установили указатель пользовательского стека
				
				LDR R1,	=thread_end
				STR R1, [R0, #20]!	;Сохраняем в стеке адрес возврата
				LDR R1,	=thread
				LDR R2, [R1]
				STR R2, [R0, #4]!	;Сохраняем в стеке PС
				MOV R2, #0x01000000	;Сохраняем в стеке флаг Thumb
				STR R2, [R0, #4]!	;Сохраняем в стеке xPSR
				
				ORR LR, LR, #0x04   ;Установим бит выбора стека процесса при возврате в поток
				BX	LR				;Выходим из обработчика используя стек процесса
from_psp
				BIC LR, LR, #0x04   ;Снимаем бит выбора стека процесса при возврате в поток
				BX	LR				;Выходим из обработчика используя главный стек
from_int		
				LDR R0, =PSP_TOP	;Загрузили адрес пользовательского стека	
				SUB R0, R0, #0x20	;Резервируем место в стеке под возврат из прерывания(8 двойных слов)
				MSR PSP, R0			;Установили указатель стека процесса
				
				LDR R1,	=thread_end
				STR R1, [R0, #24]!	;Сохраняем в стеке PС
				MOV R2, #0x01000000	;Сохраняем в стеке флаг Thumb
				STR R2, [R0, #4]!	;Сохраняем в стеке xPSR
				BX	LR				;Выходим из обработчика
				ENDP

thread_end		PROC				;Попадаем сюда при завершении потока
				EXPORT thread_end	
				LDR R1, =ICSR_ADDR	;Загружаем адрес PENDSV прерывания
				LDR R0, =PENDSVSET  ;Значения для этого регистра-адреса
				STR R0, [r1]        ;Включаем pendsv - копируя 10000000 в регистр PENDSVSET					
				B	.
				ENDP
					
				IMPORT  thread
					
				END